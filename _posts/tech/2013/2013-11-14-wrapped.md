---
title: «Обернутые» функции в Free Pascal
description: >-
  Бывает, требуется множество подпрограмм единообразно обернуть — добавить им всем одинаковое начало и завершение или,
  что более интересно, выполнить их в блоке „try ... except“ с каким-нибудь фильтром на исключения...
category: [ tech, programming, fpc ]
tags:
  - метапрограммирование
  - замыкания
image: _src/2013/11/fpc/screen.png
---
{% image @_src/2013/11/fpc/fpc.png figure right shape width=256px fig_class="logo" link=false %}

Бывает, требуется множество подпрограмм единообразно обернуть — добавить им всем одинаковое начало и завершение или,
что более интересно, выполнить их в блоке „`try ... except`“ с каким-нибудь фильтром на исключения... Вообще, понятно,
что FPC поддерживает процедурные типы, но как раз типы тут могут быть самые разные, а нетипизированный вызов с руч­ной
установкой стека — это потенциальный неиссякаемый источник трудноуловимых ошибок.

Тут самое время вспомнить, что FPC с некоторых пор поддерживает еще и вложенные процедурные типы, а вложенная процедура
имеет доступ ко всей области видимости внешней, включая, естественно, аргументы и псевдопеременную `result`.

Соответственно, можно вынести все действия подпрограммы во вложенную процедуру без па­ра­мет­ров, а в ос­нов­ной блок
вставить вызов «оборачивателя» с этой вложенной процедурой. Далее пример.

<!--more-->

## Определение обертки

Выводим на консоль исключение, если оно произошло, с пометкой времени.

{% highlight pascal %}
{$mode objfpc}
{$modeswitch nestedprocvars}
{$longstrings on}

unit Logger;

interface

uses
  SysUtils;

type
  TCode = procedure is nested;

procedure wrapper (const code : TCode);

implementation

procedure wrapper (const code : TCode);
 begin
  try
   code();
  except
    on e : Exception do
       begin
        WriteLn('[' + e.ClassName + ' at ' + DateTimeToStr(Now) + ']: ' +
                e.Message);
        raise;
       end;
  end;
 end;

end.
{% endhighlight %}

Замечу, что полученное исключение мы отправляем далее по стеку, не подавляем.

## Использование

{% highlight pascal %}
{$mode objfpc}
{$modeswitch nestedprocvars}
{$longstrings on}

program Test;

uses
  Logger;

function divide (x, y : Double) : Double;

 procedure _;
  begin
   result := x / y;
  end;

 begin
  wrapper(@_);
 end;

var
  x : Double;

begin
 try
  x := divide(1.1, 2.0);
  WriteLn(x:5:3);
  x := divide(1.1, 0.0);
  WriteLn(x:5:3);
 except
 end;
end.
{% endhighlight %}
